apply plugin: 'maven-publish'
apply plugin: 'com.android.library'

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

def getVersionName = { ->
    return "1.1.1"
}

def getArtifactId = { ->
    return "zoomy"
}

publishing {
    publications {
        zoomy(MavenPublication) {
            groupId 'com.bigskyhealth.zoomy'
            artifactId getArtifactId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtifactId()}-release.aar")

            pom {
                name = 'Zoomy'
                description = 'Zoomy is an easy to use pinch-to-zoom Android library'
                url = 'https://github.com/bigskyhealth/Zoomy'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ablanco'
                        name = 'Alvaro Blanco'
                        email = 'alvaro.bl91@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git@github.com:bigskyhealth/Zoomy.git'
                    developerConnection = 'scm:git:ssh:git@github.com:bigskyhealth/Zoomy.git'
                    url = 'https://github.com/bigskyhealth/Zoomy'
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/bigskyhealth/Zoomy")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion '29.0.3'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 2
        versionName "1.1.1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
}
